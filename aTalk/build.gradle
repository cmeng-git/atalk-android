buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        // Do not change to 3.5.0 => AbractMethodException in Note-3 (see gradle.properties)
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath "org.anarres.jarjar:jarjar-gradle:1.0.1" // working jarjar.repackage version
    }
}

apply plugin: 'com.android.application'
apply plugin: 'org.anarres.jarjar'

repositories {
    maven {
        url 'third_party/m2'
    }
    google()
    jcenter()
    mavenCentral()
    maven {
        // Need manual cleanup for new SNAPSHOT update if same name with date change only:
        // Smack official SNAPSHOT repository accepted by F-Droid
        url 'https://oss.sonatype.org/content/repositories/snapshots'

        // Smack custom library for org.igniterealtime - not recognize by F-Droid
        // url 'https://igniterealtime.org/repo/'

        // Custom library for org.jitsi - maven-metadata.xml not compatible
        // Unable to load Maven meta-data from https://github.com/jitsi/jitsi-maven-repository/tree/master/snapshots/org/jitsi/ice4j/2.0.0-SNAPSHOT/maven-metadata.xml.
        // org.xml.sax.SAXParseException; lineNumber: 44; columnNumber: 91; Attribute name "data-pjax-transient" associated with an element type "meta" must be followed by the ' = ' character.
        // url 'https://github.com/jitsi/jitsi-maven-repository/tree/master/snapshots'
    }
    maven {
        // Git project library as local library project (ucrop) - see https://jitpack.io/docs/
        url 'https://jitpack.io'
    }
}

configurations {
    fdroidImplementation
    playstoreImplementation

    // libraries duplication conflict
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    all*.exclude group: 'xpp3', module: 'xpp3'
}

configurations.all {
    it.resolutionStrategy {
        eachDependency { details ->
            //multiple actions can be specified
            if (details.requested.group == "org.igniterealtime.smack" && ['smack-experimental', 'smack-core', 'smack-extensions'].contains(details.requested.name)) {
                //changing the name:
                details.useTarget group: details.requested.group, name: "smack-jarjared", version: details.requested.version
            }
        }
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    defaultConfig {
        applicationId "org.atalk.android"
        versionCode 1110
        versionName "2.1.0"

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.compileSdkVersion
        multiDexEnabled true
    }

    flavorDimensions("distribution")
    productFlavors {
        playstore {
            dimension "distribution"
        }

        fdroid {
            dimension "distribution"
        }
    }

    buildTypes {
        release {
            // cannot enable for aTalk string resource as not all are per android style
            shrinkResources false
            // too many missing references when enable
            minifyEnabled false
            // useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
        }
    }

    File cfgFile = project.rootProject.file("settings.signing")
    if (cfgFile != null && cfgFile.exists()) {
        println "### Found settings.signing to prepare signingConfig for release signing"
        signingConfigs {
            release {
                Properties config = new Properties()
                config.load(new FileInputStream(cfgFile))

                storeFile file(config.getProperty('keystore'))
                File propsFile = file(config.getProperty('secure_properties'))
                if (propsFile.exists()) {
                    Properties props = new Properties()
                    props.load(new FileInputStream(propsFile))
                    storePassword props.getProperty('key.store.password')
                    keyAlias props.getProperty('key.store.alias')
                    keyPassword props.getProperty('key.alias.password')
                }
            }
        }
        buildTypes.release.signingConfig signingConfigs.release
    }

    sourceSets {
        main {
            resources.srcDirs = ['src']

            // disable gradle automatic ndk-build call, which ignore our Android.mk
            jni.srcDirs = []

            // This is necessary if you have pre-compiled libraries .jar/.so in your project.
            jniLibs.srcDirs = ['libs']
            java.srcDirs = ['src/main/java']
        }
        release { java.srcDirs = ['src/release/java'] }
        debug { java.srcDirs = ['src/debug/java'] }
    }

    lintOptions {
        abortOnError false
        lintConfig file("lint.xml")
        // disable 'MissingTranslation', 'InvalidPackage', 'MissingQuantity', 'AppCompatResource'
    }

    packagingOptions {
        exclude 'jni/**'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }
}

ext {
    // Need manual cleanup for new SNAPSHOT update if same name with date change only:
    // ~\.gradle\caches\modules-2\files-2.1\org.igniterealtime.smack
    // Remove source reference in .idea/libraries if source mismatched with class jar

    // for official maven release
    // smackVersion = '4.4.0-alpha2'

    // For smack own repository SNAPSHOT release
    smackVersion = '4.4.0-alpha3-SNAPSHOT' // 20190916.073603
}

dependencies {
    // Java compilation dependency: ndkBuild & jarRepackage (avoid class conflict)
    // generated files in libs/$(TARGET_ARCH_ABI)/*.so and libs/*.jar
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.legacy:legacy-support-v4:${rootProject.supportLibraryVersion}"
    implementation "androidx.legacy:legacy-support-v13:${rootProject.supportLibraryVersion}"
    implementation "androidx.appcompat:appcompat:1.1.0"
    // implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "com.google.android.material:material:${rootProject.supportLibraryVersion}"

    // libraries from maven repository
    implementation 'ch.imvs:sdes4j:1.1.4'
    // for android better picker
    implementation 'com.code-troopers.betterpickers:library:3.1.0'
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    implementation('com.jakewharton.timber:timber:4.7.1') {
        transitive = false
    }
    implementation 'com.karumi:dexter:5.0.0'

    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    // implementation 'org.jitsi:ice4j:2.0.0-SNAPSHOT';
    implementation 'com.github.yalantis:ucrop:2.2.2'

    implementation 'com.googlecode.libphonenumber:libphonenumber:8.10.5'
    implementation 'com.splitwise:tokenautocomplete:2.0.8'

    // when targeting Android API 23 and newer - class obsoleted
    implementation 'cz.msebera.android:httpclient:4.4.1.2' // duplicate class
    implementation 'de.cketti.library.changelog:ckchangelog:1.2.2'

    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'org.apache.james:apache-mime4j-core:0.8.2'
    implementation 'org.bitlet:weupnp:0.1.4'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.61'
    // (dependencies = core + prov)

    // Use by provisioning Discover
    // implementation 'org.jitsi:dnssecjava:1.1.3'

    // Required for provisioning discovery
    implementation 'org.jmdns:jmdns:3.5.3'
    implementation 'org.opentelecoms.sdp:sdp-api:1.0'

    // Smack 4.4.0-alpha2 has moved to use minidns v0.4.0-alpha3
    implementation "org.igniterealtime.smack:smack-android-extensions:$smackVersion"
    implementation "org.igniterealtime.smack:smack-bosh:$smackVersion"
    implementation "org.igniterealtime.smack:smack-compression-jzlib:$smackVersion"
    implementation "org.igniterealtime.smack:smack-core:$smackVersion"
    implementation "org.igniterealtime.smack:smack-experimental:$smackVersion"
    implementation "org.igniterealtime.smack:smack-extensions:$smackVersion"
    implementation "org.igniterealtime.smack:smack-im:$smackVersion"
    implementation "org.igniterealtime.smack:smack-legacy:$smackVersion"
    implementation "org.igniterealtime.smack:smack-resolver-minidns:$smackVersion"
    implementation "org.igniterealtime.smack:smack-sasl-provided:$smackVersion"
    implementation "org.igniterealtime.smack:smack-tcp:$smackVersion"

    implementation "org.igniterealtime.smack:smack-omemo:$smackVersion"
    implementation "org.igniterealtime.smack:smack-omemo-signal:$smackVersion"

    implementation 'org.jxmpp:jxmpp-jid:0.7.0-alpha5'
    // (dependencies = jxmpp-core + jxmpp-stringprep-libidn')

    implementation 'org.jetbrains:annotations-java5:15.0'
    implementation 'org.osgi:osgi.core:7.0.0'

    // not compatible to 17.0.0 androidx#Fragment
    playstoreImplementation "com.google.android.gms:play-services-location:${rootProject.gmsPlayServicesVersion}"
    playstoreImplementation "com.google.android.gms:play-services-maps:${rootProject.gmsPlayServicesVersion}"
    playstoreImplementation "com.google.android.gms:play-services-panorama:${rootProject.gmsPlayServicesVersion}"

    /*
     * jarjar.repackage with generated dir/filename specified - default in build/jarjar/jarjar-xxx.jar
     * filename must not contains colon i.e. ":" colon, treated as URL and will failed missing classes
     * destinationName may use relative i.e. "../../libs/fileName.jar to $buildDir/jarjar
     * classDelete must check actual jar to see if end ** is required - due to multiple components
     * May use className and className$** to delete only the related, but excluding classes with same prefix
     */
    implementation jarjar.repackage {
        from files('lib/bundles/fmj-1.0-SNAPSHOT.jar')
        destinationDir new File("${projectDir}/libs")
        destinationName "fmj-1.0-SNAPSHOT.jar"

        classRename 'java.awt.**', 'org.atalk.android.util.java.awt.@1'
        classRename 'java.beans.**', 'org.atalk.android.util.java.beans.@1'
        classRename 'javax.swing.**', 'org.atalk.android.util.javax.swing.@1'
        classRename 'javax.sound.**', 'org.atalk.android.util.javax.sound.@1'
        classRename 'javax.imageio.**', 'org.atalk.android.util.javax.imageio.@1'

        // remove extracted modules for changes
        classDelete 'net.sf.fmj.media.RegistryDefaults'
        classDelete 'javax.media.format.VideoFormat'
        classDelete 'javax.media.Manager**'
    }

    // ice4j-2.0.0 work only with AWS disabled - otherwise hang in AWS EC2 conn.getContent()
    // System.setProperty(MappingCandidateHarvesters.DISABLE_AWS_HARVESTER_PNAME, "true")
    // Objects class only available in API-19 and above
    implementation jarjar.repackage {
        // Do not use the latest master-SNAPSHOT => not compatible with aTalk
        // use only ba7e30c189 i.e. the release version of ice4j-2.0.0-20181024.160538-12.jar
        from('com.github.jitsi:ice4j:ba7e30c18980556f126e5e72ecbdb637616dd1bf') {
            transitive = false
        }
        destinationDir new File("${projectDir}/libs")
        destinationName "ice4j-2.0.0-SNAPSHOT.jar"

        classRename 'java.util.Objects', 'org.jivesoftware.smack.util.Objects'
    }

    // implementation 'com.github.jitsi:zrtp4j:master-SNAPSHOT'
    implementation jarjar.repackage {
        // Source not found either in master-SNAPSHOT or
        // from('com.github.jitsi:zrtp4j:beb6723b2e13e27710e38ea9b64f2da8d9a395a3') {
        //    transitive = false
        // }
        from files('lib/bundles/zrtp4j-light-4.1.0-jitsi-1-SNAPSHOT.jar')
        destinationDir new File("${projectDir}/libs")
        destinationName "zrtp4j-light-4.1.0.jar"

        classRename 'org.jitsi.**', 'org.atalk.@1'
    }
}

task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
    def ndkDir = android.ndkDirectory
    def jniSrcDir = projectDir.toString() + "\\jni"

    println '****************************************************************************************'
    println "Main jni directory for NDK build => " + jniSrcDir
    println '****************************************************************************************'

    // APP_PLATFORM=android-21 for aTalk minimum SDK platform support i.e. API-21
    // this setting if set takes priority over value set in jni/Application.mk
    commandLine "$ndkDir/ndk-build",
            '-C' + projectDir + '/jni',
            '-j', Runtime.runtime.availableProcessors(),
            'NDK_OUT=' + project.buildDir,
            'NDK_LIBS_OUT=' + projectDir + '/libs/$(TARGET_ARCH_ABI)'
    //'V=1'  // enable only for compilation debug only
}

task ndkClean(type: Exec, description: 'Clean JNI object files') {
    def ndkDir = android.ndkDirectory
    commandLine "$ndkDir/ndk-build",
            '-C' + projectDir + '/jni',
            'clean'
}

task updateVersionFile(type: Copy) {
    description '<!-- Generate versionupdate.properties file -->'

    def versionName = android.defaultConfig.versionName
    def versionCode = android.defaultConfig.versionCode
    def date = new Date()
    // ******************************************
    println "*** " + description
    println "# Date: " + date
    println "# last_version = " + versionName
    println "# last_version_code = " + versionCode

    outputs.upToDateWhen {
        false
    }
    from('lib/versionupdate.properties') {
        // Substitute property tokens in files
        expand(DATE: date, BUILD_VERSION: versionName, BUILD_VERSION_CODE: versionCode)
    }
    into 'build/outputs/apk'
}

// cmeng - need to perform setup libs (first time & after clean)
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkBuild
}

// will clean /libs/armeabi/*.so
clean.dependsOn 'ndkClean'
build.dependsOn 'updateVersionFile'
